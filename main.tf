provider "aws" {
  region = "us-east-1"
}

resource "aws_s3_bucket" "resume_site" {
  bucket = "zangi-cloud-resume-site-2026"

  tags = {
    Name        = "CloudResumeSite"
    Environment = "Dev"
    Project     = "CloudResumeChallenge"
    Owner       = "Zangi"
  }
}

resource "aws_s3_bucket_website_configuration" "resume_config" {
  bucket = aws_s3_bucket.resume_site.id

  index_document {
    suffix = "index.html"
  }
}

resource "aws_s3_bucket_public_access_block" "public_access" {
  bucket = aws_s3_bucket.resume_site.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

resource "aws_s3_bucket_policy" "allow_public" {
  bucket = aws_s3_bucket.resume_site.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Sid       = "PublicReadGetObject"
      Effect    = "Allow"
      Principal = "*"
      Action    = "s3:GetObject"
      Resource  = "${aws_s3_bucket.resume_site.arn}/*"
    }]
  })

  depends_on = [aws_s3_bucket_public_access_block.public_access]
}

resource "aws_cloudfront_distribution" "resume_cdn" {

  origin {
    domain_name = aws_s3_bucket_website_configuration.resume_config.website_endpoint
    origin_id   = "S3-Website"

    custom_origin_config {
      http_port              = 80
      https_port             = 443
      origin_protocol_policy = "http-only"

      origin_ssl_protocols = ["TLSv1.2"]
    }
  }
  aliases = [
    "syedkcloudops.com",
    "www.syedkcloudops.com"
  ]

  enabled             = true
  default_root_object = "index.html"

  default_cache_behavior {

    allowed_methods = ["GET", "HEAD"]

    cached_methods = ["GET", "HEAD"]

    target_origin_id = "S3-Website"

    viewer_protocol_policy = "redirect-to-https"

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn      = "arn:aws:acm:us-east-1:027527476154:certificate/2dfaad18-4055-4951-bf93-d86162b03d57"
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.2_2021"
  }

  tags = {
    Name        = "ResumeCDN"
    Environment = "Dev"
    Project     = "CloudResumeChallenge"
  }
}

